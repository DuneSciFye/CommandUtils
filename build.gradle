plugins {
    id 'java'
    id "com.gradleup.shadow" version "8.3.3"
}

group = 'me.dunescifye'
version = '2.10.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }

    maven { url = "https://repo.codemc.org/repository/maven-public/" } // Item NBT API

    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    maven {
        url = 'https://maven.playpro.com'
    }
    maven {
        name = "jeff-media"
        url = "https://hub.jeff-media.com/nexus/repository/jeff-media-public/"
        content {
            includeGroup "com.jeff-media"
        }
    }
    maven { url = "https://s01.oss.sonatype.org/content/repositories/snapshots" }
    maven {
        name = "jitpack"
        url = "https://jitpack.io/"
        content {
            includeGroupByRegex "com\\.github\\..*"
        }
    }
    maven {
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        url = "https://dependency.download/releases"
    }
    maven {
        url = "https://maven.enginehub.org/repo/" // WorldGuard
    }
    flatDir(
            dirs: 'libs' // LeafAPI
    )
    maven {
        url = "https://mvn.lib.co.nz/public"
        content {
            includeGroup("me.libraryaddict.disguises")
        }
    }
}

dependencies {

    compileOnly "io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT" // Default API for shared code

    compileOnly 'me.clip:placeholderapi:2.11.5'
    compileOnly 'de.tr7zw:item-nbt-api-plugin:2.12.3'
    compileOnly 'com.github.GriefPrevention:GriefPrevention:16.18.2'
    compileOnly "net.kyori:adventure-text-serializer-legacy:4.16.0"
    compileOnly 'net.coreprotect:coreprotect:22.4'
    compileOnly("me.libraryaddict.disguises:libsdisguises:11.0.0")
    compileOnly 'com.github.SaberLLC:Saber-Factions:4.1.4-STABLE'
    compileOnly fileTree(dir: 'libs', include: ['*.jar']) // LibsDisguises, LeafAPI (local JARs)
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.9' // WorldGuard
    implementation 'com.jeff-media:MorePersistentDataTypes:2.4.0'
    implementation 'com.jeff-media:custom-block-data:2.2.3'
    implementation "dev.jorel:commandapi-paper-shade:11.1.0" // CommandAPI
    implementation 'dev.dejvokep:boosted-yaml:1.3.5'
}

shadowJar {
    relocate 'com.jeff_media.customblockdata', 'me.dunescifye.libs.customblockdata'
    relocate 'com.jeff_media.morepersistentdatatypes', 'me.dunescifye.libs.morepersistentdatatypes'
    relocate 'dev.jorel.commandapi', 'me.dunescifye.libs.commandutils.commandapi'
    relocate 'dev.dejvokep.boostedyaml', 'me.dunescifye.libs'
    archiveBaseName.set("CommandUtils")
    archiveClassifier.set("") // removes the "-all" suffix
    archiveVersion.set("")    // removes the version number
}

subprojects {
    apply plugin: 'java'

    group = 'me.dunescifye'
    version = '1.0'

    repositories {
        mavenCentral()
    }

}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks.jar {
    manifest {
        attributes["paperweight-mappings-namespace"] = "mojang"
    }
}
// if you have shadowJar configured
tasks.shadowJar {
    manifest {
        attributes["paperweight-mappings-namespace"] = "mojang"
    }
}